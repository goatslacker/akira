jison = require: 'jison'

unwrap = /^function\s*\(\)\s*\{\s*return\s*([\s\S]*)\s*\}/

getMatch = \action -> {
  x = unwrap.exec: action
  if x then x.2 else '({{action}}())'
}

addYY = \str -> str.replace: /new /g, 'new yy.'
addLine = \str -> 'yy.L(yylineno, ' ++ (str.replace: ';', '') ++ ');'

o = \pattern, action -> {
  pattern-string = pattern.replace: /\s{2,}/g, ' '
  if not action
    then [pattern-string, '$$ = $1']
    else {
      newaction = action | getMatch | addYY | addLine
      [pattern-string, '$$ = {{newaction}}']
    }
}

bnf = ({
  Root = [
    ['', 'return $$ = new yy.Nodes;']
    ['Program', 'return $$ = $1']
  ]

  Program = [
    (o: 'Terminator Expressions', (\-> $2))
    (o: 'Expressions')
  ]

  Expressions = [
    (o: 'Body', (\-> yy.Nodes.wrap: $1))
    (o: 'Expressions TERMINATOR Body', (\-> $1.push: $3))
    (o: 'Expressions TERMINATOR')
  ]

  Terminator = [
    (o: 'TERMINATOR')
    (o: 'Terminator TERMINATOR')
  ]

  OptTerminator = [
    (o: '')
    (o: 'Terminator')
  ]

  Identifier = [
    (o: 'IDENTIFIER', (\-> clone Identifier: $1))
  ]

  IdentifierNoUtils = [
    (o: 'IDENTIFIER', (\-> clone Identifier: $1, true))
  ]

  Prototype = [
    (o: 'PROTOTYPE', (\-> clone Identifier: $1))
  ]

  Rest = [
    (o: '. . . IDENTIFIER', (\-> clone Identifier: $4, false, true))
  ]

  Body = [
    (o: 'Expression')
    (o: '{ OptTerminator Expressions }', (\-> $3))
  ]

  Expression = [
    (o: 'Modules')
    (o: 'Assignable')
    (o: 'Exceptions')
    (o: 'Objects')
  ]

  Assignable = [
    (o: 'Value')
    (o: 'If')
    (o: 'Assignment')
    (o: 'Declaration')
    (o: 'PatternMatching')
    (o: 'Invocation')
    (o: 'Pipeline')
    (o: 'Cloning')
    (o: 'Comprehensions')
    (o: 'Prototype')
    (o: 'IMPORT STR', (\-> clone Import: $2))
  ]

  Value = [
    (o: 'Literal')
    (o: 'Lists')
    (o: 'Maps')
    (o: 'Operation')
    (o: 'Access')
    (o: 'Type')
    (o: '@')
    (o: 'SpecialFunctions')
    (o: '( Assignable )', (\-> $2))
  ]

  Values = [
    (o: 'Value')
    (o: 'Invocation')
  ]

  Type = [
    (o: 'TYPE', (\-> clone Identifier: $1))
  ]

  Literal = [
    (o: 'Identifier')
    (o: 'AlphaNumeric')
    (o: 'NONE', (\-> clone Literal: none))
    (o: 'BOOL', (\-> clone Literal: ($1 == 'true')))
    (o: 'REGEXP', (\-> clone Literal: $1))
  ]

  AlphaNumeric = [
    (o: 'NUM', (\-> clone Literal: (Number: $1)))
    (o: 'Strings')
  ]

  Strings = [
    (o: 'STR', (\-> clone Literal: (String: $1)))
  ]

  Assignment = [
    (o: 'Identifier = Assignable', (\-> clone Assignment: $1, $3))
    (o: 'Access = Assignable', (\-> clone Assignment: $1, $3))
    (o: 'Lists = Assignable', (\-> clone Assignment: $1, $3))
    (o: 'Maps = Assignable', (\-> clone Assignment: $1, $3))
  ]

  Objects = [
    (o: 'OBJECT Identifier OptClones Maps', (\-> clone Objects: $2, $3, $4))
  ]

  OptClones = [
    (o: '', (\-> none))
    (o: 'CLONES Identifier', (\-> $2))
  ]

  Modules = [
    (o: 'EXPORT Assignable', (\-> clone Export: $2))
  ]

  Cloning = [
    (o: 'CLONE Invocation', (\-> clone Construction: $2))
  ]

  Pipeline = [
    (o: 'Value | OptTerminator Values', (\-> clone Pipeline: $1, $4))
    (o: 'Invocation | OptTerminator Values', (\-> clone Pipeline: $1, $4))
    (o: 'Pipeline | OptTerminator Values', (\-> clone Pipeline: $1, $4))
  ]

  Invocation = [
    (o: 'Value : Arguments', (\-> clone Call: $1, $3))
    (o: 'Value !', (\-> clone Call: $1))
  ]

  Declaration = [
    (o: '\ Parameters LAMBDA Body', (\-> clone Declaration: none, $2, $4))
    (o: '\ LAMBDA Body', (\-> clone Declaration: none, none, $3))
  ]

  Pattern = [
    (o: 'Value ? Body TERMINATOR', (\-> [$1, $3] ))
  ]

  Patterns = [
    (o: 'Pattern', (\-> clone Arguments: $1))
    (o: 'Patterns Pattern', (\-> clone Arguments: $1, $2))
  ]

  Default = [
    (o: 'ELSE ? Body TERMINATOR', (\-> clone Arguments: [$1, $3]))
  ]

  PatternMatching = [
    (o: '\ Parameters LAMBDA TERMINATOR Patterns Default',
      (\-> clone Declaration: none, $2, (clone Pattern: $2, $5, $6)))
  ]

  Comma = [
    (o: ', OptTerminator')
  ]

  Comprehensions = [
    (o: 'NUM . . Numbers ComprehensionBy ComprehensionFunction',
      (\-> clone Comprehension: $1, $4, $5, $6))
    (o: 'Identifier . . Numbers ComprehensionBy ComprehensionFunction',
      (\-> clone Comprehension: $1, $4, $5, $6))
  ]

  Numbers = [
    (o: 'NUM')
    (o: 'Identifier')
  ]

  ComprehensionFunction = [
    (o: '', (\-> none))
    (o: ', Declaration', (\-> $2))
  ]

  ComprehensionBy = [
    (o: '', (\-> clone Literal: 1))
    (o: 'Numbers')
  ]

  Lists = [
    (o: '[ ]', (\-> clone List!))
    (o: '[ OptTerminator ListArgs OptTerminator ]', (\-> clone List: $3))
  ]

  ListArgs = [
    (o: 'Arg', (\-> clone Arguments: $1))
    (o: 'ListArgs Comma Arg', (\-> clone Arguments: $1, $3))
    (o: 'ListArgs Terminator Arg', (\-> clone Arguments: $1, $3))
  ]

  KeyValue = [
    (o: 'IdentifierNoUtils', (\-> clone Assignment: $1, $1))
    (o: 'IdentifierNoUtils = Value', (\-> clone Assignment: $1, $3))
  ]

  MapItems = [
    (o: 'KeyValue', (\-> clone Arguments: $1))
    (o: 'MapItems Comma KeyValue', (\-> clone Arguments: $1, $3))
    (o: 'MapItems Terminator KeyValue', (\-> clone Arguments: $1, $3))
  ]

  Maps = [
    (o: '( { } )', (\-> clone Map!))
    (o: '( { OptTerminator MapItems OptTerminator } )',
      (\-> clone Map: ({ obj = $4 })))
  ]

  Access = [
    (o: 'Identifier Accessor', (\-> clone Access: $1, $2))
    (o: '@ Accessor', (\-> clone Access: $1, $2))
    (o: 'Prototype Accessor', (\-> clone Access: $1, $2))
    (o: 'Type Accessor', (\-> clone Access: $1, $2))
    (o: 'Access Accessor', (\-> clone Access: $1, $2))
  ]

  Accessor = [
    (o: '. IdentifierNoUtils', (\-> $2))
    (o: '. AlphaNumeric', (\-> $2))
  ]

  Arg = [
    (o: 'Value')
    (o: 'Rest')
  ]

  Arguments = [
    (o: '()', (\-> none))
    (o: 'Arg', (\-> clone Arguments: $1))
    (o: 'Arguments Comma Arg', (\-> clone Arguments: $1, $3))
  ]

  Param = [
    (o: 'IdentifierNoUtils')
    (o: 'IdentifierNoUtils = Value', (\-> clone Assignment: $1, $3))
    (o: 'Rest')
    (o: 'Lists')
  ]

  Parameters = [
    (o: 'Param', (\-> clone Arguments: $1))
    (o: 'Parameters , Param', (\-> clone Arguments: $1, $3))
  ]

  Exceptions = [
    (o: 'TRY Body OptTerminator CATCH Body', (\-> clone Exception: $2, $5))
    (o: 'RAISE Value ErrorType', (\-> clone Exception: $1, $2, $3))
  ]

  ErrorType = [
    (o: '', (\-> none))
    (o: 'Identifier')
  ]

  If = [
    (o: 'IF Values OptTerminator THEN Body OptTerminator ELSE Body',
      (\-> clone If: $2, $5, $8))
  ]

  MathOperators = [
    (o: '+', (\-> clone Identifier: '_add'))
    (o: '-', (\-> clone Identifier: '_sub'))
    (o: '*', (\-> clone Identifier: '_mul'))
    (o: '/', (\-> clone Identifier: '_div'))
  ]

  SpecialFunctions = [
    (o: '( MathOperators )', (\-> $2)),
    (o: '( MathOperators Value )',
      (\-> clone Call: (clone Call: (clone Identifier: 'curryr'),
        (clone Arguments: $2)), (clone Arguments: $3)))
  ]

  Operation = [
    (o: 'Value COMPARE Value', (\-> clone Compare: $1, $2, $3))
    (o: 'Value LOGIC Value', (\-> clone Compare: $1, $2, $3))
    (o: 'Value LEFT_OPERATORS Value', (\-> clone Operation: $1, $2, $3))
    (o: 'Value RIGHT_OPERATORS Value', (\-> clone Operation: $1, $2, $3))
    (o: 'NOT Value', (\-> clone Compare: $2, $1))
    (o: 'Value + Value', (\-> clone Operation: $1, '+', $3))
    (o: 'Value - Value', (\-> clone Operation: $1, '-', $3))
    (o: 'Value * Value', (\-> clone Operation: $1, '*', $3))
    (o: 'Value / Value', (\-> clone Operation: $1, '/', $3))
  ]
})


tokens = [
  'ASSERT', 'IF', 'THEN', 'ELSE',
  'LAMBDA',
  'IDENTIFIER', 'BOOL', 'NUM', 'STR',
  'LOGIC', 'COMPARE',
  'TERMINATOR'
]

operators = [
  ['right', 'NOT']
  ['right', 'IF', 'THEN', 'ELSE', 'IMPORT', 'EXPORT']
  ['right', '=', '|']
  ['right', 'RIGHT_OPERATORS']
  ['nonassoc', '(', ')', '[', ']', '{', '}', ':']
  ['left', 'LOGIC']
  ['left', 'COMPARE']
  ['left', 'LEFT_OPERATORS']
  ['left', '+', '-']
  ['left', '*', '/']
]

Parser = jison.Parser
grammar = ({
  tokens = tokens
  bnf = bnf
  operators = operators
  startSymbol = 'Root'
})

parser = clone Parser: grammar
lexer = ({
  lex = (\-> {
    ref = this.tokens !! this.pos || ['']
    this.pos = this.pos + 1
    this.yytext = ref !! 1
    this.yylineno = ref !! 2
    ref !! 0
  })
  set-input = (\tokens -> {
    this.tokens = tokens
    this.pos = 0
  })
})
parser.lexer = lexer

export parser
