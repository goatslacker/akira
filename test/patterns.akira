numbers = fn [n]
  'one' ? 1
  'two' ? 2
  'three' ? 3
  else ? 4

ok: 4, (numbers: 'four')
ok: 1, (numbers: 'one')

gtTen = fn [n]
  n < 10 ? true
  n > 10 ? false
  else ? none

ok: false, (gtTen: 11)
ok: true, (gtTen: 2)

starts-with = fn [noun, prefix] prefix == noun.1 + noun.2

spanish-gender-of-noun = fn [noun]
  (starts-with: noun, 'el') ? 'male'
  (starts-with: noun, 'la') ? 'female'
  else ? none

ok: 'female', (spanish-gender-of-noun: 'la casa')
ok: 'male', (spanish-gender-of-noun: 'el carro')

sort-even-odd = fn [[x & xs]]
  not x ? []
  else ? {
    if (odd: x)
      then (sort-even-odd: xs) ++ [x]
      else x +: (sort-even-odd: xs)
  }

[1 2 3 4 5 6] | sort-even-odd | ok-deep: [2 4 6 5 3 1]

-- implementations of our util functions
init = fn [[& a b]] a
tail = fn [[a & b]] b
head = fn [[a & b]] a
last = fn [[& a b]] b
get-middle = fn [[a & b c]] b


ls = [1 2 3 4 5]

ls | get-middle | ok-deep: [2 3 4]
ls | init | ok-deep: [1 2 3 4]
ls | tail | ok-deep: [2 3 4 5]
ls | head | ok: 1
ls | last | ok: 5

add-two = fn [(n = 0)]
  n is 0 ? 0
  else ? n + 2

add-two: 0 | ok: 0
add-two: 2 | ok: 4
